import org.gradle.internal.os.OperatingSystem

switch ( OperatingSystem.current() ) {
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
}

project.ext.lwjglVersion = "3.1.2"

repositories {
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'

dependencies {
    compile "org.lwjgl:lwjgl:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-glfw:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-jemalloc:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-openal:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-stb:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-vulkan:${lwjglVersion}"
    runtime "org.lwjgl:lwjgl:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-glfw:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-jemalloc:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-openal:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-stb:${lwjglVersion}:${lwjglNatives}"
    compile 'com.google.guava:guava:22.0'
    compile 'com.esotericsoftware:kryonet:2.22.0-RC1'
    compile 'com.esotericsoftware:kryonet:2.22.0-RC1'
    testCompile 'junit:junit:4.12'
}

mainClassName = 'Articulata'

def IDEA_BUILD_DIR = 'build'
new File(IDEA_BUILD_DIR).mkdirs()

idea {
    module {
        name = 'Articulata'
        outputDir = file(IDEA_BUILD_DIR)
        testOutputDir = file(IDEA_BUILD_DIR)
        excludeDirs += file('build')
    }

    project {
        ipr.withXml {
            xmlFile ->
                xmlFile.asNode().component.find { it.@name == 'VcsDirectoryMappings' }.replaceNode {
                    component(name: 'VcsDirectoryMappings') {
                        mapping(directory: "", vcs: "")
                        mapping(directory: "\$PROJECT_DIR\$", vcs: 'Git')
                    }
                }
                xmlFile.asNode().component.find { it.@name == 'ProjectRootManager' }['output'][0].replaceNode {
                    output(url: "file://\$PROJECT_DIR\$/$IDEA_BUILD_DIR/out")
                }


        }
    }
}